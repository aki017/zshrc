autoload -Uz add-zsh-hook
autoload -Uz vcs_info
ZLE_RPROMPT_INDENT=0
BG_HOST="027"
FG_HOST="015"
BG_NAME="007"
FG_NAME="004"
BG_MAIN="default"
FG_MAIN="015"
PROMPT=""
PROMPT="$PROMPT%K{$BG_HOST}%F{$FG_HOST} %m %f%k%K{$BG_NAME}%F{$BG_HOST}$DLH%f%k"
PROMPT="$PROMPT%K{$BG_NAME}%F{$FG_NAME} %n %f%k%K{$BG_MAIN}%F{$BG_NAME}$DLH%f%k"
PROMPT="$PROMPT%K{$BG_MAIN}%F{$FG_MAIN} "

setopt promptsubst
autoload -Uz vcs_info
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' get-revision true

FORCE_RUN_VCS_INFO=1

zstyle ':vcs_info:*+pre-get-data:*' hooks pre-get-data
+vi-pre-get-data() {
    [[ "$vcs" != git && "$vcs" != hg ]] && return

    if [[ -n $FORCE_RUN_VCS_INFO ]]; then
        FORCE_RUN_VCS_INFO=
        return
    fi

    ret=1
    case "$(fc -ln $(($HISTCMD-1)))" in
        git*)
            ret=0
            ;;
        hg*)
            ret=0
            ;;
        gst*)
            ret=0
            ;;
        vim*)
            ret=0
            ;;
    esac
}

prompt_precmd() {
    vcs_info
}
add-zsh-hook precmd prompt_precmd

prompt_chpwd() {
    FORCE_RUN_VCS_INFO=1
}
add-zsh-hook chpwd prompt_chpwd

zstyle ':vcs_info:git+set-message:*' hooks \
         git-hook-begin \
         git-untracked


function +vi-git-hook-begin() {
  if [[ $(command git rev-parse --is-inside-work-tree 2> /dev/null) != 'true' ]]; then
    return 1
  fi

  return 0
}

function +vi-git-untracked() {
  local git_status=$(command git status --porcelain 2> /dev/null)
  local m1=$(echo $git_status | cut -c 1 | grep -c -e 'M' -e 'A' -e 'D' -e 'R' -e 'C')
  local m2=$(echo $git_status | cut -c 2 | grep -c -e 'M' -e 'A' -e 'D' -e 'R' -e 'C' -e 'U')
  local m3=$(echo $git_status | cut -c 1-2 | grep -c '??')
  local branch=$(git symbolic-ref --short HEAD)
  local up=$(command git rev-list origin/master..master 2>/dev/null|wc -l)
  local down=$(command git rev-list master..origin/master 2>/dev/null|wc -l)
  hook_com[misc]=""
  local prev_color=$BG_MAIN
  if [[ $m1 -ne 0 ]]; then
    hook_com[misc]+="%K{$prev_color}%F{121}$DRH%k%f%K{121}%F{022}●$m1 %k%f"
    prev_color="121"
  fi
  if [[ $m2 -ne 0 ]]; then
    hook_com[misc]+="%K{$prev_color}%F{225}$DRH%k%f%K{225}%F{196}✚ $m2 %k%f"
    prev_color="225"
  fi
  if [[ $m3 -ne 0 ]]; then
    hook_com[misc]+="%K{$prev_color}%F{051}$DRH%k%f%K{051}%F{053}…$m3 %k%f"
    prev_color="051"
  fi
  hook_com[misc]+="%K{$prev_color}%F{024}$DRH%k%f%K{024}%F{015} $branch %k%f"
  if [[ $up -ne 0 ]]; then
    hook_com[misc]+="$DRS↑$up "
  fi
  if [[ $down -ne 0 ]]; then
    hook_com[misc]+="$DRS↓$down "
  fi
}
zstyle ':vcs_info:git*' formats "%m"
zstyle ':vcs_info:git*' actionformats "$DRS%a%m"
RPROMPT='${vcs_info_msg_0_:-}'
